import groovy.json.JsonOutput
import groovy.json.JsonSlurper

plugins {
    id 'com.diffplug.spotless'
    id 'java-library'
    id 'maven-publish'
    id 'idea'
}

base {
    archivesName = "${mod_id}-${project.name}-${minecraft_version}"
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(java_version)
}

repositories {
    mavenCentral()
    // https://docs.gradle.org/current/userguide/declaring_repositories.html#declaring_content_exclusively_found_in_one_repository
    exclusiveContent {
        forRepository {
            maven {
                name = 'Sponge'
                url = 'https://repo.spongepowered.org/repository/maven-public'
            }
        }
        filter { includeGroupAndSubgroups("org.spongepowered") }
    }
    maven {
        name = 'BlameJared'
        url = 'https://maven.blamejared.com'
    }
    maven { url "https://api.modrinth.com/maven" }
    maven { url "https://maven.terraformersmc.com/" }
    maven { url "https://maven.terraformersmc.com/releases" } // modmenu
    maven { url 'https://libs.azuredoom.com:4443/mods' }
    maven { url "https://cfa2.cursemaven.com" }
    maven { url 'https://api.modrinth.com/maven' }
    maven {
        name = 'Parchment'
        url = 'https://maven.parchmentmc.org'
    }
}

dependencies {
    implementation 'org.jetbrains:annotations:24.1.0'
}

['apiElements', 'runtimeElements'].each { variant ->
    configurations."$variant".outgoing {
        capability("$group:$mod_id-${project.name}-${minecraft_version}:$version")
        capability("$group:$mod_id:$version")
    }
    publishing.publications.configureEach {
        suppressPomMetadataWarningsFor(variant)
    }
}

jar {
    from(rootProject.file("LICENSE")) {
        rename { "${it}_${mod_name}" }
    }

    manifest {
        attributes([
                'Specification-Title'   : mod_name,
                'Specification-Vendor'  : mod_author,
                'Specification-Version' : project.jar.archiveVersion,
                'Implementation-Title'  : project.name,
                'Implementation-Version': project.jar.archiveVersion,
                'Implementation-Vendor' : mod_author,
                'Built-On-Minecraft'    : minecraft_version
        ])
    }
}

processResources {
    def expandProps = [
            "version": version,
            "group": project.group,
            "minecraft_version": minecraft_version,
            "minecraft_version_range": minecraft_version_range,
            "fabric_version": fabric_version,
            "fabric_loader_version": fabric_loader_version,
            "mod_name": mod_name,
            "mod_author": mod_author,
            "mod_id": mod_id,
            "mod_logo": mod_logo,
            "mod_license": mod_license,
            "mod_description": mod_description,
            "mod_issues": mod_issues,
            "mod_sources": mod_sources,
            "mod_url": mod_url,
            "loader_version_range": loader_version_range,
            "neo_version": neo_version,
            "neo_version_range": neo_version_range,
            "mod_credits": mod_credits,
            "java_version": java_version,
            "azurelib_version": azurelib_version
    ]

    filesMatching(['pack.mcmeta', 'fabric.mod.json', 'META-INF/neoforge.mods.toml', '*.mixins.json']) {
        expand expandProps
    }
    inputs.properties(expandProps)

    doLast {
        def jsonMinifyStart = System.currentTimeMillis()
        def jsonMinified = 0
        def jsonBytesSaved = 0
        fileTree(dir: outputs.files.asPath, include: '**/*.json').each {
            File file = it
            jsonMinified++
            def oldLength = file.length()
            file.text = JsonOutput.toJson(new JsonSlurper().parse(file))
            jsonBytesSaved += oldLength - file.length()
        }
        println('Minified ' + jsonMinified + ' json files. Saved ' + jsonBytesSaved + ' bytes. Took ' + (System.currentTimeMillis() - jsonMinifyStart) + 'ms.')
    }
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}

spotless {
    java {
        eclipse().configFile("$rootDir/eclipse-formatter.xml")
        endWithNewline()
        importOrder("", "java", group.toString(), "\\#")
        indentWithSpaces(4)
        removeUnusedImports()
        trimTrailingWhitespace()
    }
}

tasks.build {
    dependsOn("spotlessApply")
}